/*
   FALCON - Generic Parser
   FILE: html.fal

   Generates an html file out of an input genparser.Context tree
   -------------------------------------------------------------------
   Author: Giancarlo Niccolai
   Begin: Sat, 30 Aug 2008 09:42:22 +0200

   -------------------------------------------------------------------
   (C) Copyright 2008: the FALCON developers (see list in AUTHORS file)

   See LICENSE file for licensing details.
*/

import SiteHelper from .sitehelper as SiteHelper

/*# Default renderer with basic functionalities.
*/
class BaseSiteHelper from SiteHelper
   function code(v, lang)
      return "<pre>\n"+v.replace("&","&amp;").replace( "<","&lt;").replace(">", "&gt;" )+"</pre>"
   end

   function pageLink( v )
      return "/"+URI.encode(v)
   end

   function iLink( wiki, v )
      return @"http://www.$(wiki).com/wiki/" + URI.encode(v)
   end

   function img( v )
      return v
   end

   function file( v )
      return v
   end

   function plugin( v, params, node )
      return @"<p>Placeholder for plugin \"$v\"($params)</p>"
   end
end


/*# Html Renderer.
   @param siteHelper A site helper in the class of 
   @param cls Class used to render block elements (for easier CSS application).

   
*/
class Renderer( siteHelper, cls )
   cls = cls ? cls : ""
   site = siteHelper ? siteHelper : BaseSiteHelper()

   _btype = nil
   _blevel = 0

   renderers = [
      "text" => {node => node.content},
      "b" => self._render_markup,
      "i" => self._render_markup,
      "u" => self._render_markup,
      "sup" => self._render_markup,
      "sub" => self._render_markup,
      "pre" => self._render_markup,
      "tt" => self._render_markup,
      "ol" => self._render_markup,
      "ul" => self._render_markup,
      "li" => self._render_markup,

      "hr" => self._render_break,
      "br" => self._render_break,

      "para" => self._render_para,
      "header" => self._render_header,

      "table" => self._render_table,
      "tr" => self._render_tr,
      "th" => self._render_cell,
      "td" => self._render_cell,
      "indent" => self._render_indent,
      "quote" => self._render_quote,

      "link" => self._render_link,
      "file" => self._render_file,

      "img" => self._render_img,
      "code" => self._render_code,
      "plugin" => self._render_plugin,
      "tag" => self._render_tag
   ]


   function render( context, page_name )
      self.site.page_name = page_name
      self.site.topnode = context.topNode

      //preprocess standouts
      self.preprocess( context.standouts )

      // and return the content
      return self.rcont( context.topNode )
   end

   function preprocess( sout )
      i = 0
      soutlen = sout.len()
      while i < soutlen
         node_sout = sout[i++]
         if node_sout.type == "plugin"
            self.site.plugin( node_sout.infos["name"], node_sout.infos["params"], node_sout, true )
         end
      end
   end


   function renderNode( node )
      if node.type in self.renderers
         return self.renderers[ node.type ]( node )
      else
         return self._renderUNKNOWN( node )
      end
   end


   function rcont( node )
      content = node.content
      if content: return content
      
      text = ""
      node = node.firstChild
      while node
         text += self.renderNode( node )
         node = node.next
      end
      return text
   end

   //==========================================================
   // rendering functions
   //

   function _renderUNKNOWN( node )
      return self._render_markup(node)
   end

   function _ct( tag )
      if self.cls
         return "<" + tag + " class=\""+self.cls+"\">"
      else
         return "<" + tag + ">"
      end
   end

   function _render_para( node )
      return self._ct("p") + self.rcont(node)+"</p>\n"
   end

   function _render_markup( node )
      return "<" + node.type + ">" + self.rcont(node) + "</" + node.type + ">"
   end

   function _render_break( node )
      return "<" + node.type + "/>\n"
   end


   function _render_header( node )
      content = self.rcont( node )
      return  "<a name=\"" + URI.encode(content)+"\">"+
               self._ct("h" + node.level) +
               content +
               "</h" + node.level + "></a>\n\n"
   end


   function _render_table( node )
      return self._ct("table") + "\n<tbody>\n" +
              self.rcont(node) +
              "</tbody></table>\n"
   end

   function _render_tr( node )
      return "<tr>"+ self.rcont(node) + "</tr>\n"
   end


   function _render_cell( node )
      return self._render_markup( node )
   end

   function _render_indent( node )
      return self._ct("div style=\"margin-left:" + 2 * node.level + "em\"") + self._ct("p") +
         self.rcont(node) +
         "</p></div>\n"
   end

   function _render_link( node )
      i = node.infos.pseudo
      link = "<a href=\""
      if i.ext
         link += i.name.escape() + "\" target=\"_blank\""
      else
         if "ilink" in i and i.ilink
            link +=  self.site.iLink( i.ilink, i.name ) + "\" target=\"_blank\""
         else
            if "internal" in i and i.internal
               link +=  "#" + URI.encode( i.name ) +"\""
            else
               link += self.site.pageLink( i.name ) +"\""
            end
         end
      end

      link += ">" + (i.text ? i.text : i.name) + "</a>"
      return link
   end


    function _render_file( node )
      i = node.infos.pseudo
      link = "<a href=\"" + self.site.file( i.name ) + "\" target=\"_blank\">" +
         (i.text ? i.text : i.name) + "</a>"
      return link
   end

   function _render_quote( node )
      i = node.infos.pseudo
      if i.link
         str = self._ct("blockquote cite=\"" + i.link.escape() + "\" ")
      else
         str = self._ct("blockquote")
      end
      str += self.rcont(node) +"</blockquote>\n"
      return str
   end

   function _render_img( node )
      i = node.infos.pseudo
      link = ""
      style = ""
      
      if "thumb" in i
         link +="<a href=\"" + self.site.img( i.name ) +"\" target=\"_blank\">"
      end

      link += "<img src=\"" + self.site.img( i.name ) + "\""
      if i.alt: link += " alt=\"" + i.alt + "\""
      if "w" in i: link+=" width=\""+i.w+"\""
      if "h" in i: link+=" height=\""+i.h+"\""
      if "valign" in i: style +="vertical-align:"+i.valign+";"
      if "pad" in i: style +="margin:"+i.pad+"px;"
      if "center" in i: style +="display: block; margin-left: auto; margin-right: auto;"
      if style: link +=" style=\""+style+"\""      
      link += "/>"

      if "thumb" in i: link +="</a>"
      return link
   end

   function _render_code( node )
      return self.site.code( self.rcont(node), node.infos["lang"] )
   end

   function _render_plugin( node )
      return self.site.plugin( node.infos["name"], node.infos["params"], node )
   end

   function _render_tag( node )
      text = ""
      for k,v in node.infos
         forfirst: text += " "
         text += k +"=\"" + v.escape() + "\""
         formiddle: text += " "
      end
      return "<" + node.tag + text + ">" + self.rcont( node ) + "</" + node.tag +">\n"
   end
   
end

