/*
   FALCON - Generic Parser

   FILE: generic.fal

   Read faldoc blocks inside generic files.
   -------------------------------------------------------------------
   Author: Giancarlo Niccolai
   Begin: Sat, 25 Sep 2010 14:53:06 +0200

   -------------------------------------------------------------------
   (C) Copyright 2010: the FALCON developers (see list in AUTHORS file)

   See LICENSE file for licensing details.
*/


import InfoNode from parser.genparser.node as InfoNode

import TagRule from parser.genparser.rules as TagRule
import ReRule from parser.genparser.rules as ReRule
import DummyRule from parser.genparser.rules as DummyRule

import Parser from parser.genparser in parser.genparser
import PState from parser.genparser as PState


class Parser from parser.genparser.Parser
   states = [
      "start" => PState( .[
            ReRule( '/\*#[*#\s]?', "grab", {ctx=> ctx.pushNode( InfoNode( "faldoc_txt", ["line"=>ctx.row] )) })
            ReRule( '//#\s*(?:@)', "grab_eol", {ctx=> ctx.pushNode( InfoNode( "faldoc_txt", ["line"=>ctx.row] )) })

            TagRule( "/*", "comment" )
            ReRule( "//.*$", "#stay" )
         ],
         "*" // get all
      ),

      "grab" =>PState(.[
            TagRule( "\\*", "#stay", {ctx=> ctx.add("*")} )
            ReRule( '\s*\*+/', "#pop", {ctx=> ctx.popNode()})

            // Consumes useless * at begin of comment
            ReRule( '^\s*\*+', "#stay" )
         ],
         {ctx, data=>ctx.add( data )},
         "grab_continue"
      ),

      "grab_continue"=> PState(
         .[ DummyRule( "#pop", { ctx => ctx.add("\n")} ) ]
      ),

      "grab_eol" =>PState(
         .[],
         {ctx, data=>ctx.add( data )},
         "grab_eol_checker"
      ),

      "grab_eol_checker" =>PState(
         .[
            // if there is a //# on the next line, go on returning on the prev state
            TagRule( "//#", "#pop", {ctx=>ctx.add("\n")} )
            // Else, pop the node and return to top
            DummyRule( "#pop;#pop", {ctx=>ctx.popNode()} )
         ]
      ),

      "comment" =>PState(.[
            ReRule( '.*?\*/', "#pop" )
            ReRule( '.*', "#stay" )
         ]
      ),

      // Just to be able to add always "," at the end
      "*dummy" => nil
   ]

   function reset()
      self.initParser( "start" )
   end
end


